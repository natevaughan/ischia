buildscript {
	ext.spring_version = '2.7.3'
	ext.spring_security_version = '5.7.3'
	ext.kotlin_version = '1.7.10'
	ext.junit_version = '4.12'
	ext.mockito_version = '1.10.19'
	ext.slf4j = [:]
	ext.slf4j.version = '1.7.28'

	repositories {
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}

plugins {
	id 'org.springframework.boot' version '2.7.0'
	id "org.jetbrains.kotlin.jvm" version '1.7.10'
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	implementation project(':modules:1.domain')
	implementation project(':modules:1.util')
	implementation project(':modules:2.api')
	implementation project(':modules:3.service')

	implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

	implementation("org.springframework.boot:spring-boot-starter-web:$spring_version") {
		exclude group: "org.springframework.boot", module: "spring-boot-starter-tomcat"
	}
	implementation "org.springframework.boot:spring-boot-starter-jetty:$spring_version"
	implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.2'
	implementation "org.springframework.boot:spring-boot-starter-cache:$spring_version"

	implementation "org.springframework.boot:spring-boot-starter-security:$spring_version"
	implementation "org.springframework.security:spring-security-oauth2-client:$spring_security_version"
	implementation "org.springframework.security:spring-security-oauth2-jose:$spring_security_version"

	implementation "org.springframework.boot:spring-boot-starter-data-jpa:$spring_version"
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:2.7.2'

	implementation("com.google.guava:guava:31.1-jre")

	implementation "org.slf4j:slf4j-api:${slf4j.version}"
	implementation "javax.inject:javax.inject:1"
}

compileKotlin {
	kotlinOptions {
		jvmTarget = "11" // or "1.8" for Java 8
	}
}
compileTestKotlin {
	kotlinOptions {
		jvmTarget = "11" // or "1.8" for Java 8
	}
}

group = 'io.ischia'
version = '0.2.4'

allprojects {

	sourceCompatibility = 11 // or "1.8" for Java 8
	targetCompatibility = 11 // or "1.8" for Java 8

	repositories {
		mavenLocal()
		mavenCentral()
	}
}

subprojects {
	apply plugin: "org.jetbrains.kotlin.jvm"

	dependencies {
		implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
		implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

		implementation "org.slf4j:slf4j-api:${slf4j.version}"

		implementation 'org.liquibase:liquibase-core:3.8.1'
		implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.2' // needed for liquibase + JDK 11

		testImplementation "junit:junit:$junit_version"
		testImplementation "org.mockito:mockito-all:$mockito_version"
	}
}